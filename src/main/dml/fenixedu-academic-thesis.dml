package org.fenixedu.academic.thesis.domain;

 // class definition

class ThesisProposalsSystem {
    	protected int proposalsCounter;
    	public LocalizedString acceptExternalColaborationTerms;
    	public LocalizedString acceptEthicsAndDataProtection;
}

class ThesisProposalsConfiguration {
	public Interval proposalPeriod;
	public Interval candidacyPeriod;
	public int maxThesisCandidaciesByStudent;
	public int maxThesisProposalsByUser;
	public int minECTS1stCycle;
	public int minECTS2ndCycle;
}

class ThesisProposal {
    protected String identifier;
 	public String title;
 	public String observations;
 	public String requirements;
 	public String goals;
 	public String localization;
 	public boolean hidden;
    public boolean externalColaboration;
    public String externalInstitution;
    public boolean acceptExternalColaborationTerms;
    public boolean acceptEthicsAndDataProtection;
    public boolean isCapstone;
    public int minStudents;
    public int maxStudents;
 }

class StudentThesisCandidacy {
 	public int preferenceNumber;
 	public boolean acceptedByAdvisor;
 	protected DateTime timestamp;
 }

class ThesisProposalParticipant {
    public int participationPercentage;
}

class ThesisProposalParticipantType {
    public LocalizedString name;
    public int weight;
    public boolean inactive;
}

class reports.ThesisProposalsReportFile extends .org.fenixedu.academic.domain.reports.GepReportFile {}

class ExternalUser {
    public String name;
    public String email;
}

class ProposalsLog {
	protected DateTime timestamp;
	public String action;
	public String thesisProposal;
	public String candidacy;
}

// access groups relationships definition

relation ThesisCreatorsGroup {
    protected ThesisProposalsSystem playsRole rootAsThesisCreatorsGroup;
    protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole thesisCreatorsGroup {
        multiplicity 1..1;
    }
}

relation ThesisSystemManager {
  protected ThesisProposalsSystem playsRole rootAsThesisSystemManager;
  protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole thesisSystemManager {
    multiplicity 1..1;
  }
}

relation DegreeThesisManager {
  protected .org.fenixedu.academic.domain.Degree playsRole rootAsThesisManager;
  protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole thesisManager {
    multiplicity 0..1;
  }
}

 // root relationships definition
relation ThesisProposalsSystemBennuRoot {
	public ThesisProposalsSystem playsRole thesisProposalsSystem {
		multiplicity 0..1;
	}
	public .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
}

relation ThesisProposalsSystemThesisProposalsConfiguration {
	public ThesisProposalsSystem playsRole thesisProposalsSystem {
		multiplicity 1..1;
	}
	public ThesisProposalsConfiguration playsRole thesisProposalsConfiguration {
		multiplicity 0..*;
	}
}

relation ThesisProposalsSystemThesisProposals {
 	public ThesisProposalsSystem playsRole thesisProposalsSystem {
 		multiplicity 1..1;
 	}
 	public ThesisProposal playsRole thesisProposals {
 		multiplicity 0..*;
 	}
 }

relation ThesisProposalsSystemThesisParticipantTypes {
	public ThesisProposalsSystem playsRole thesisProposalsSystem {
		multiplicity 1..1;
	}
	public ThesisProposalParticipantType playsRole thesisProposalParticipantType {
		multiplicity 0..*;
	}
}

relation ThesisProposalsSystemStudentThesisCandidacy {
 	public ThesisProposalsSystem playsRole thesisProposalsSystem {
 		multiplicity 1..1;
 	}
 	public StudentThesisCandidacy playsRole studentThesisCandidacy {
 		multiplicity *;
 	}
 }

 // relationships definition

relation ThesisProposalsSystemExecutionDegree {
 	public ThesisProposalsConfiguration playsRole thesisProposalsConfiguration {
 		multiplicity 0..*;
 	}
  	public .org.fenixedu.academic.domain.ExecutionDegree playsRole executionDegree {
 		multiplicity 1..1;
 	}
}

relation ThesisProposalParticipant {
 	public ThesisProposal playsRole thesisProposal {
 		multiplicity 1..1;
 	}
	public ThesisProposalParticipant playsRole thesisProposalParticipant  {
 		multiplicity 1..*;
 	}
 }

relation ThesisProposalConfiguration {
	public ThesisProposal playsRole thesisProposal {
		multiplicity 0..*;
	}
	public ThesisProposalsConfiguration playsRole thesisConfiguration {
		multiplicity 1..*;
	}
}

relation ThesisProposalParticipantUser {
    public .org.fenixedu.bennu.core.domain.User playsRole user {
    multiplicity 0..1;
    }

    public ThesisProposalParticipant playsRole thesisProposalParticipant {
    multiplicity 0..*;
    }
}

relation ThesisProposalParticipantExternalUser {
    public ExternalUser playsRole externalUser {
    multiplicity 0..1;
    }

    public ThesisProposalParticipant playsRole thesisProposalParticipant {
    multiplicity 1..1;
    }
}

relation ThesisProposalParticipantParticipantType {
    public ThesisProposalParticipantType playsRole thesisProposalParticipantType {
	multiplicity 1..1;
    }

    public ThesisProposalParticipant playsRole thesisProposalParticipant {
	multiplicity 0..*;
    }
}

 relation ThesisProposalsStudentCandidacies {
 	public ThesisProposal playsRole thesisProposal {
 		multiplicity 1..1;
 	}
 	protected StudentThesisCandidacy playsRole studentThesisCandidacy {
 		multiplicity 0..*;
 	}
 }

 relation StudentThesisCandidacyStudent {
 	public StudentThesisCandidacy playsRole studentThesisCandidacy {
 		multiplicity 0..*;
 	}
 	public .org.fenixedu.academic.domain.student.Registration playsRole registration {
 		multiplicity 1..1;
 	}
 }
 
 // Log relationships
 
 relation ProposalsLogAuthor {
 	public .org.fenixedu.bennu.core.domain.User playsRole author {
 		multiplicity 1..1;
 	}
 	public ProposalsLog playsRole logger {
 		multiplicity 0..*;
 	}
 }

